import java.nio.file.Files
import java.util.jar.JarEntry
import java.util.jar.JarFile

plugins() {
	id('fabric-loom') version('1.11-SNAPSHOT')
	//id('maven-publish')
}

version = project.mod_version
group   = project.maven_group

repositories() {
	//Add repositories to retrieve artifacts from in here.
	//You should only use this when depending on other mods because
	//Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	//See https://docs.gradle.org/current/userguide/declaring_repositories.html
	//for more information about repositories.

	maven() {
		url('https://api.modrinth.com/maven')
	}

	//cardinal components
	maven() {
		url('https://maven.ladysnake.org/releases')
	}

	//AutoCodec
	mavenLocal()

	//manifold
	maven() {
		url('https://oss.sonatype.org/content/repositories/snapshots/')
	}

	//computercraft
	maven() {
		url('https://maven.squiddev.cc')
		content() {
			includeGroup("cc.tweaked")
		}
	}

	//loom does this automatically, but does it too late.
	//we need to ensure this gets added as a repository before
	//all the hacks below that make JIJ dependencies work.
	maven() {
		name = 'MavenCentralLWJGL' //loom checks for this name, so it cannot be changed.
		url(ArtifactRepositoryContainer.MAVEN_CENTRAL_URL)
		content() {
			includeGroup('org.lwjgl')
		}
	}
}

configurations() {
	jijModImplementation() {
		canBeResolved = true
	}
	jijModRuntimeOnly() {
		canBeResolved = true
	}
	jijModCompileOnly() {
		canBeResolved = true
	}
}

static void extractNestedJars(File dependenciesFolder, File toUnpack) {
	try (JarFile jar = new JarFile(toUnpack)) {
		Enumeration<JarEntry> entries = jar.entries()
		while (entries.hasMoreElements()) {
			JarEntry entry = entries.nextElement()
			String name = entry.getName()
			if (name.startsWith('META-INF/jars/') && name.endsWith('.jar')) {
				name = name.substring('META-INF/jars'.length())
				File unpacked = new File(dependenciesFolder, name)
				if (unpacked.exists()) continue
				try (FileOutputStream out = new FileOutputStream(unpacked)) {
					jar.getInputStream(entry).transferTo(out)
				}
				extractNestedJars(dependenciesFolder, unpacked)
			}
		}
	}
}
dependencies() {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	//mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	mappings(fileTree("dependencies/${project.minecraft_version}/mappings"))
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	include(implementation('builderb0y:autocodec:6.0.2') {
		transitive = false
	})

	modImplementation("cc.tweaked:cc-tweaked-1.21.8-fabric-api:1.116.1")

	include(modImplementation('org.ladysnake.cardinal-components-api:cardinal-components-base:7.0.0-beta.1'))
	include(modImplementation('org.ladysnake.cardinal-components-api:cardinal-components-world:7.0.0-beta.1'))
	include(modImplementation('org.ladysnake.cardinal-components-api:cardinal-components-chunk:7.0.0-beta.1'))

	include(modImplementation('maven.modrinth:fractal-lightning:LqXhZ6tG'))

	jijModImplementation("maven.modrinth:cloth-config:19.0.147+fabric")
	jijModImplementation("maven.modrinth:modmenu:15.0.0")
	jijModImplementation("maven.modrinth:lambdynamiclights:4.3.3+1.21.8")

	//not necessary, I just like playing with these.
	jijModRuntimeOnly("maven.modrinth:logical-zoom:0.0.32")
	jijModRuntimeOnly("maven.modrinth:sodium:mc1.21.6-0.6.13-fabric")
	jijModRuntimeOnly("maven.modrinth:lithium:mc1.21.8-0.18.0-fabric")

	//manifold.
	String manifoldVersion = '2025.1.25'
	annotationProcessor("systems.manifold:manifold-strings:$manifoldVersion")

	annotationProcessor("systems.manifold:manifold-ext:$manifoldVersion")
	include(implementation("systems.manifold:manifold-ext-rt:$manifoldVersion"))

	include(implementation("systems.manifold:manifold-rt:$manifoldVersion"))
	include(implementation("systems.manifold:manifold-util:$manifoldVersion"))

	testImplementation('junit:junit:4.13.2')

	File dependenciesFolder = file('jijDependencies')
	dependenciesFolder.deleteDir()
	dependenciesFolder.mkdir()
	for (String dependencyType : [ 'implementation', 'runtimeOnly', 'compileOnly' ]) {
		File specificDependenciesFolder = new File(dependenciesFolder, dependencyType)
		specificDependenciesFolder.mkdir()
		for (File from : getConfigurations().getByName("jijMod${ dependencyType.capitalize() }").getResolvedConfiguration().getResolvedArtifacts().file) {
			String fileName = from.getName()
			File to = new File(specificDependenciesFolder, fileName)
			if (!to.exists()) {
				Files.copy(from.toPath(), to.toPath())
				extractNestedJars(specificDependenciesFolder, to)
			}
		}
	}

	modImplementation(fileTree(file('jijDependencies/implementation')))
	modCompileOnly(fileTree(file('jijDependencies/compileOnly')))
	modRuntimeOnly(fileTree(file('jijDependencies/runtimeOnly')))
}

test() {
	useJUnit()
}

loom() {
	accessWidenerPath = file('src/main/resources_core/bigtech.accesswidener')
}

sourceSets() {
	main() {
		resources() {
			srcDir(file('src/main/resources_core'))
		}
	}
}

compileJava() {
	options.compilerArgs.addAll([ '-parameters', '-Xplugin:Manifold no-bootstrap' ])
}

processResources() {
	inputs.property('version', project.version)
	inputs.property('minecraft_version', project.minecraft_version)
	inputs.property('loader_version', project.loader_version)
	filteringCharset('UTF-8')

	filesMatching('fabric.mod.json') {
		expand(
			'version': project.version,
			'minecraft_version': project.minecraft_version,
			'loader_version': project.loader_version
		)
	}
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach() {
	//ensure that the encoding is set to UTF-8, no matter what the system default is
	//this fixes some edge cases with special characters not displaying correctly
	//see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	//If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = 'UTF-8'
	if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
		it.options.release = targetJavaVersion
	}
}

java() {
	def javaVersion = JavaVersion.toVersion(targetJavaVersion)
	if (JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
	}
	archivesBaseName = project.archives_base_name
	//Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	//if it is present.
	//If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar() {
	from('LICENSE') {
		rename() { "${it}_${project.archivesBaseName}" }
	}
}

task copyToReleases(type: Copy) {
	dependsOn(remapJar)
	from("$projectDir/build/libs") {
		include("$archivesBaseName-${project.version}.jar")
		rename() {
			"$archivesBaseName-$project.version-MC${minecraft_version}.jar"
		}
	}
	into(file("releases/${project.version.toString().replace('.' as char, '/' as char)}"))
}

build() {
	dependsOn(copyToReleases)
}

/*
//configure the maven publication
publishing() {
	publications() {
		mavenJava(MavenPublication) {
			from(components.java)
		}
	}

	//See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories() {
		//Add repositories to publish to here.
		//Notice: This block does NOT have the same function as the block in the top level.
		//The repositories here will be used for publishing your artifact, not for
		//retrieving dependencies.
	}
}
*/